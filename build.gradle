/*
 * Copyright 2017-2018 Red Hat, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish' version '0.9.10'
  id 'org.jetbrains.kotlin.jvm' version '1.2.51'
  id 'com.github.ben-manes.versions' version '0.20.0'
}

repositories {
  jcenter()
  mavenLocal()
}

version = '0.2.0-SNAPSHOT'
group = 'io.vertx'

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.51'
  implementation 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
  implementation ('com.netflix.nebula:nebula-dependency-recommender:5.2.0') {
    exclude group: 'org.jetbrains.kotlin'
  }

  testImplementation 'junit:junit:4.12'
  testImplementation 'com.mashape.unirest:unirest-java:1.4.9'
  testImplementation 'org.assertj:assertj-core:3.10.0'
}

gradlePlugin {
  plugins {
    vertxPlugin {
      id = 'io.vertx.vertx-plugin'
      implementationClass = 'io.vertx.gradle.VertxPlugin'
    }
  }
}

pluginBundle {
  website = 'https://github.com/jponge/vertx-gradle-plugin'
  vcsUrl = 'https://github.com/jponge/vertx-gradle-plugin'
  plugins {
    vertxPlugin {
      id = 'io.vertx.vertx-plugin'
      displayName = 'Vert.x Gradle Plugin'
      description = 'An opinionated Gradle plugin for Vert.x projects'
      tags = ['vertx', 'vert.x', 'reactive', 'microservice']
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.8.1'
  distributionType = 'all'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}
